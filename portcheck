#!/usr/bin/perl -w

use strict;
use IO::Socket::INET;
use IO::Socket;
use Getopt::Long;
use Pod::Usage;
use Term::ANSIColor

$| = 1;
my $errs = 0;
my $timeout = 2;
my $port = 389;
my $help = 0;

GetOptions ("timeout=i" => \$timeout,
            "port=i"    => \$port,
            "help|?"    => \$help) or pod2usage(1);

pod2usage(0) if $help;

while (<>) {

	my $hostname = $_ or die("Missing input\n");
	chomp $hostname;

	my @host = gethostbyname($hostname);
	my ($name, $aliases, $addrtype, $length, @packed_ips) = @host;

	print color('bold yellow');
	print "Host: $name:$port\n";
	print color('reset');
	
	foreach my $packed_ip (@packed_ips) {
		my $ip = inet_ntoa($packed_ip);
		
		print color('reset');
		printf("%-15s", $ip);

		my $socket = new IO::Socket::INET(
			PeerHost => $ip,
			PeerPort => $port,
			Proto => 'tcp',
			Timeout => $timeout
		);

		if ($socket) {
			print color('bold green');
			print "[OK]\n";
		} else {
			$errs++;
			print color('bold red');
			print "[EE]\n";
		}
		print color('reset');
	}
	print "\n";
}

exit $errs;

__END__

=head1 NAME

portcheck - Quick and dirty portchecker

=head1 SYNOPSIS

        portcheck [OPTIONS]

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exit.

=item B<--timeout> n

Connection timeout in seconds.
Default: 2 seconds.

=item B<--port> n

Connection port. 
Default: 389.

=back

=cut
